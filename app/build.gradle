apply plugin: 'com.android.application'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'

if (file("play-store-api.json").exists()) {
    apply plugin: 'com.github.triplet.play'
    play {
        serviceAccountCredentials = file("play-store-api.json")
        track = "beta"
        userFraction = 1.0
        resolutionStrategy = "ignore"
    }
}

def xmpushProperties = new Properties()
def conf = project.file("xmpush.properties")
if (conf.exists()) xmpushProperties.load(conf.newDataInputStream())

if (xmpushProperties['fabric.key'] != null) {
    apply plugin: 'io.fabric'
}

android {
    compileSdkVersion 28
    defaultConfig {
        def packageName = xmpushProperties['app.id']
        if (packageName == null) packageName = System.getenv("PACKAGE_NAME")
        applicationId packageName
        minSdkVersion 21
        targetSdkVersion 28
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        def appId = xmpushProperties['appId']
        if (appId == null) appId = System.getenv("XM_APP_ID")
        def appKey = xmpushProperties['appKey']
        if (appKey == null) appKey = System.getenv("XM_APP_KEY")
        buildConfigField "String", "XM_APP_ID", "\"" + appId + "\""
        buildConfigField "String", "XM_APP_KEY", "\"" + appKey + "\""
        def fabricKey = xmpushProperties['fabric.key']
        if (fabricKey == null) fabricKey = System.getenv("FABRIC_KEY")
        if (fabricKey == null) fabricKey = "disabled"
        buildConfigField "String", "FABRIC_KEY", "\"" + fabricKey + "\""
        manifestPlaceholders.fabricKey = fabricKey
    }
    signingConfigs {
        general {
            def locate = xmpushProperties['key.locate']
            def keystorePwd = xmpushProperties['key.store.pwd']
            def alias = xmpushProperties['key.alias']
            def pwd = xmpushProperties['key.pwd']
            storeFile file(locate != null ? locate : project.rootProject.file(".yuuta.jks"))
            storePassword keystorePwd != null ?
                    keystorePwd : System.getenv("KEYSTORE_PASS")
            keyAlias alias != null ?
                    alias : System.getenv("ALIAS_NAME")
            keyPassword pwd != null ?
                    pwd : System.getenv("ALIAS_PASS")
            v2SigningEnabled false
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.general
        }
        release {
            signingConfig signingConfigs.general
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dataBinding {
        enabled = true
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha01'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
    implementation 'com.elvishew:xlog:1.6.1'
    implementation project(':common')
    implementation 'moe.shizuku.preference:preference:3.0.0'
    implementation 'moe.shizuku.preference:preference-dialog-android:3.0.0'
    implementation 'moe.shizuku.preference:preference-simplemenu:3.0.0'
    implementation 'com.google.android.material:material:1.1.0-alpha02'
    def nav_version = "1.0.0-alpha08"
    implementation "android.arch.navigation:navigation-fragment:$nav_version"
    implementation "android.arch.navigation:navigation-ui:$nav_version"
    // Retrofit & RxJava
    def retrofitVersion = "2.5.0"
    implementation"com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation 'com.google.android.gms:play-services-oss-licenses:16.0.1'
    // Fabric
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.8'
}
