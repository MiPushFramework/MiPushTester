buildscript {
    repositories {
        google()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.1'
        classpath 'com.google.android.gms:oss-licenses-plugin:0.9.4'
        classpath "com.github.triplet.gradle:play-publisher:2.0.0-rc2"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'

if (file("play-store-api.json").exists()) {
    apply plugin: 'com.github.triplet.play'
    play {
        serviceAccountCredentials = file("play-store-api.json")
        track = "beta"
        userFraction = 1.0
        resolutionStrategy = "ignore"
    }
}

def xmpushProperties = new Properties()
def conf = project.file("xmpush.properties")
if (conf.exists()) xmpushProperties.load(conf.newDataInputStream())

repositories {
    google()
    mavenCentral()
    jcenter()
}

android {
    compileSdkVersion 28
    defaultConfig {
        def packageName = xmpushProperties['app.id']
        if (packageName == null) packageName = System.getenv("PACKAGE_NAME")
        applicationId packageName
        minSdkVersion 21
        targetSdkVersion 28
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        def appId = xmpushProperties['appId']
        if (appId == null) appId = System.getenv("XM_APP_ID")
        def appKey = xmpushProperties['appKey']
        if (appKey == null) appKey = System.getenv("XM_APP_KEY")
        buildConfigField "String", "XM_APP_ID", "\"" + appId + "\""
        buildConfigField "String", "XM_APP_KEY", "\"" + appKey + "\""
    }
    signingConfigs {
        general {
            def locate = xmpushProperties['key.locate']
            def keystorePwd = xmpushProperties['key.store.pwd']
            def alias = xmpushProperties['key.alias']
            def pwd = xmpushProperties['key.pwd']
            storeFile file(locate != null ? locate : project.rootProject.file(".yuuta.jks"))
            storePassword keystorePwd != null ?
                    keystorePwd : System.getenv("KEYSTORE_PASS")
            keyAlias alias != null ?
                    alias : System.getenv("ALIAS_NAME")
            keyPassword pwd != null ?
                    pwd : System.getenv("ALIAS_PASS")
            v2SigningEnabled false
        }
    }
    sourceSets {
        main {
            java.srcDirs += 'src/main/kotlin'
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.general
        }
        release {
            signingConfig signingConfigs.general
            debuggable true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dataBinding {
        enabled = true
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
    kapt {
        generateStubs = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.3.0-alpha01'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    implementation 'com.google.android.material:material:1.3.0-alpha01'
    def nav_version = "1.0.0-alpha08"
    implementation 'androidx.navigation:navigation-fragment:2.3.0'
    implementation 'androidx.navigation:navigation-ui:2.3.0'
    // Retrofit & RxJava
    def retrofitVersion = "2.5.0"
    implementation"com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation 'com.google.android.gms:play-services-oss-licenses:16.0.1'
    // Stream support for Java8-
    implementation 'com.annimon:stream:1.2.1'
    // Preferences
    implementation 'moe.shizuku.preference:preference-appcompat:4.0.0'
    implementation 'moe.shizuku.preference:preference-simplemenu-appcompat:4.0.0'
    // Logger
    implementation 'com.elvishew:xlog:1.6.1'
    // Common
    implementation project(':common')
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    // Condom
    implementation 'com.oasisfeng.condom:library:2.3.1'
}
